plugins {
	id 'org.springframework.boot' version '2.7.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jvm-test-suite' // https://docs.gradle.org/current/userguide/jvm_test_suite_plugin.html
	id 'com.gorylenko.gradle-git-properties' version '2.4.1' // https://github.com/n0mer/gradle-git-properties
}

group = 'com.jjbeto.microservice'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
	maven { url 'https://repo.spring.io/milestone' }
}

ext {
	set('springCloudVersion', "2021.0.3-SNAPSHOT")
}

springBoot {
	buildInfo()
}
gitProperties {
	keys = ['git.commit.id', 'git.branch']
}

testing {
	suites {
		integrationTest(JvmTestSuite) {
			dependencies {
				implementation project
				implementation 'org.springframework.boot:spring-boot-starter-test'
			}
		}
	}
}

tasks.named('check') {
	dependsOn testing.suites.integrationTest
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'javax.cache:cache-api' // jcache
	implementation 'org.ehcache:ehcache'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
